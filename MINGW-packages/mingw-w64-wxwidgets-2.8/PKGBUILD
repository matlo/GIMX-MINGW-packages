# Usage example: makepkg MINGW_PREFIX=/mingw64 MINGW_CHOST=x86_64-w64-mingw32 CARCH=x86_64

_realname=wxMSW
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.8.12
pkgrel=1
pkgdesc="A C++ library that lets developers create applications for Windows, Linux and UNIX (mingw-w64)"
arch=('any')
license=("custom:wxWindows")
groups=("${_mingw_suff}")
url="http://www.wxwidgets.org/"
depends=("${_mingw_suff}-gcc-libs")
makedepends=("${_mingw_suff}-gcc")
options=('strip' 'staticlibs' '!buildflags')
source=("http://sourceforge.net/projects/wxwindows/files/2.8.12/wxMSW-2.8.12.tar.gz")
sha1sums=('5112bf56e5154c32433bccd09486e9d50a36e819')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  sed -i 's/ -lwctl3d32//g' configure
  sed -i 's/ -lwctl3d32//g' configure.in
  if [ "${MINGW_PREFIX}" == "/mingw64" ]
  then
    sed -i 's/--define __WIN32__ --define __WIN95__ --define __GNUWIN32__/--define WX_CPU_AMD64/g' configure
  fi
}

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  export "CXXFLAGS=-fpermissive"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --disable-shared \
    --disable-debug \
    --disable-threads \
    --enable-monolithic \
    --enable-unicode \
    --without-subdirs
  make
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
